plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 31
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
}
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.techinessoverloaded'
            artifactId = 'progress-dialog'
            version = '1.4.0a6'
            artifact sourceJar
            artifact "$buildDir/outputs/aar/progress-dialog-release.aar"

            pom {
                name = 'ProgressDialog Library'
                description = 'A ProgressDialog Library for Android API Level 24 and above. Quite Useful for showing progress during any operation.'
                url = 'https://github.com/techinessoverloaded/progress-dialog'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'techinessoverloaded'
                        name = 'Arunprasadh C'
                        email = 'techinessover@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def propsFile = rootProject.file('github.properties')
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            name = "GitHubPackages"
            url = uri(props['repouri'])
            credentials {
                username props['username']
                password props['token']
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.0-alpha01'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}